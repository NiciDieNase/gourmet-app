apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"

String appName = "Genie√üer-App"
String versionString = new File("versionfile").text.trim()

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "de.nicidienase.geniesser_app"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName versionString
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [label: appName]
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                             "$projectDir/schemas".toString()]
            }
        }

    }

    signingConfigs {
        //noinspection GroovyMissingReturnStatement, GroovyAssignabilityCheck
        release {
            if (project.hasProperty("gourmetKeyStore") && file(gourmetKeyStore).exists() && file(gourmetKeyStore).isFile()) {
                println "Release app signing is configured: will sign APK"
                storeFile file(gourmetKeyStore)
                storePassword gourmetStorePassword
                keyAlias gourmetKeyName
                keyPassword gourmetKeyPassword
            } else {
                println(gourmetKeyStore + gourmetKeyName + gourmetKeyPassword + gourmetStorePassword)
                println "App signing data not found. Will not sign."
            }
        }
    }

    buildTypes {
        debug {
        }
        release {
            useProguard true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty("gourmetKeyStore") && file(gourmetKeyStore).exists() && file(gourmetKeyStore).isFile()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "stage"
    productFlavors {
        prod {
            dimension "stage"
            buildConfigField "String", "APPCENTER_ID", "$gourmetAppcenterId"
            manifestPlaceholders = [label: appName + "-debug"]
        }
        dev {
            dimension "stage"
            applicationIdSuffix ".dev"
            manifestPlaceholders = [label: appName + "-Dev"]
            buildConfigField "String", "APPCENTER_ID", "$gourmetAppcenterDevId"
        }
    }
    dataBinding.enabled = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-M2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0-M2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    def room_version = "2.1.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    def nav_version_ktx = "2.1.0-alpha06"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version_ktx"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version_ktx"

    def appCenterSdkVersion = '2.2.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    debugImplementation 'com.facebook.stetho:stetho:1.4.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-alpha-3'

    implementation "com.squareup.retrofit2:retrofit:2.6.0"
    implementation "com.squareup.retrofit2:converter-gson:2.6.0"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
}
